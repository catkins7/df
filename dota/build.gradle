apply plugin: 'com.android.application'

tasks.whenTaskAdded { task ->
    if (task.name == "validateBetaSigning")
        task.dependsOn keystoreInfoBeta
}

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20
        versionCode 127
        versionName "0.5.11"
    }

    signingConfigs {
        beta {
            storeFile file("release.keystore")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        beta {
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            runProguard false
            signingConfig signingConfigs.beta
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile "com.squareup.picasso:picasso:2.3.4"
    compile "com.squareup.retrofit:retrofit:1.6.1"
    compile "com.squareup.okhttp:okhttp:2.0.0"
    compile "com.squareup.okhttp:okhttp-urlconnection:2.0.0"

    compile 'com.android.support:support-v13:19.1.0'
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}

task keystoreInfoBeta << {
    def console = System.console()
    if (console == null)
        throw new IllegalStateException('no console available, use --no-daemon flag')

    def storeFile = console.readLine('Keystore: ')
    def storePassword = console.readPassword('Keystore password: ')
    def keyAlias = console.readLine('Key alias: ')
    def keyPassword = console.readPassword('Key password: ')

    android.signingConfigs.beta.storeFile = file(storeFile)
    android.signingConfigs.beta.storePassword = new String(storePassword)
    android.signingConfigs.beta.keyAlias = keyAlias
    android.signingConfigs.beta.keyPassword = new String(keyPassword)
}
